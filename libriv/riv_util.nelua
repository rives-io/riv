## pragmas.unitname = ''

require 'math'
require 'C.stdio'
require 'C.stdlib'
require 'C.stdarg'
require '.riv_types'

global function riv_panic(msg: cstring <const>): void <cexport,noreturn>
  if msg then
    C.fprintf(C.stderr, "PANIC: %s", msg)
  else
    C.fprintf(C.stderr, "PANIC!\n")
  end
  C.abort()
end

-- Returns RIV version as an integer.
global function riv_version(): uint64 <cexport>
  return RIV_VERSION
end

-- Returns current machine cycle (USE ONLY FOR benchmarking).
global function riv_rdcycle(): uint64 <cexport>
  local cycle: uint64 <noinit,nodce>
  cemit![[asm volatile ("rdcycle %0" : "=r" (cycle));]]
  return cycle
end

-- Prints a formatted string to stdout.
global function riv_printf(format: cstring <const>, ...: cvarargs): usize <cexport>
  local args: cvalist
  C.va_start(args, format)
  local ret: cint = C.vprintf(format, args)
  C.va_end(args)
  if ret < 0 then ret = 0 end
  return ret
end

-- Formats a string.
global function riv_snprintf(buf: cstring, n: usize, format: cstring <const>, ...: cvarargs): usize <cexport>
  local args: cvalist
  C.va_start(args, format)
  local ret: cint = C.vsnprintf(buf, n, format, args)
  C.va_end(args)
  if ret < 0 then ret = 0 end
  return ret
end

-- Formats a temporary string.
global function riv_tprintf(format: cstring <const>, ...: cvarargs): cstring <cexport>
  local maxlen: usize = #riv.temp_str_buf - riv.temp_str_off
  if maxlen == 0 then return (@cstring)(&riv.temp_str_buf[#riv.temp_str_buf-1]) end
  local args: cvalist
  C.va_start(args, format)
  local buf: cstring = (@cstring)(&riv.temp_str_buf[riv.temp_str_off])
  local ret: cint = C.vsnprintf(buf, maxlen, format, args)
  C.va_end(args)
  if ret <= 0 then
    return (@cstring)(&riv.temp_str_buf[#riv.temp_str_buf-1])
  end
  riv.temp_str_off = riv.temp_str_off + (@usize)(ret + 1)
  return buf
end

-- Check if a key was pressed or continues to be pressed at repeat rate.
global function riv_is_key_repeat_press(key: uint8, repeat_delay: uint64, repeat_rate: uint64): boolean <cexport>
  if key >= RIV_NUM_KEYCODE then return false end
  if riv.keys[key].press then return true end
  if not riv.keys[key].down then return false end
  if repeat_delay == 0 then repeat_delay = math.max(riv.target_fps // 3, 1) end
  if repeat_rate == 0 then repeat_rate = 20 end
  local repeat_interval: uint64 = math.max(riv.target_fps // repeat_rate, 1)
  local down_frame: uint64 = riv.keys[key].down_frame
  return riv.frame >= down_frame + repeat_delay and (riv.frame - down_frame) % repeat_interval == 0
end
